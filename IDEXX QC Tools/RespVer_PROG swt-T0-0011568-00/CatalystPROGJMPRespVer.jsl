//Author(s)
//NClark
//IDEXX Laboratories, Inc.
//Westbrook, ME 04092
//nathan-clark@idexx.com

//Tool to help decide correct titer for PROG Assay

//------------------Revision History -------------------------------
//
//	1.3.0 NClark 091521
//		-> Commentd out 'close window' that breaks JMP 15 (closing table removes variable forcing error, JMP 14 does not react same)
//	1.2.0.0 NClark 092718
//		-> Updated so criteria is for doses 1, 5, and 20 (not 0.5, 1, 30)
//		-> Negative SDs will cause failure of verification
//		-> Added section to determine full script to be run (and not just old way)
//			-> relies on specific text within filename
//			-> Sets both type of report AND specifications
//			-> Added dashed line for relaxed specifications along with text
//		-> Incorporated that check so specific items not run when not desired
//	1.1.0.0 NClark 091718
//		-> Updated Text for Reponse failure
//	1.0.0.1 NClark 091218
//		-> Updated response check to be based on dose model and not avg of dose and titer models
//	1.0 NClark 072817
//		-> Updated SDMA to PROG
//	Ported from v1.3.0.1 OPS SDMA Titer Response Verification
//------------------------------------------------------------------
Names Default To Here( 1 );	

//named because JMP has a built in Sequence function
nrcSequence = Function({startValue,endValue,increment},{default Local},
	seqList = {};
	For(i=startValue,i<endValue + increment,i+=increment,
		insertInto(seqList,i)
	);
	Return(seqList)
);
missingCols = Function( {tableFail},{tableFail},
	If(tableFail == "Both",
		tableFail = "Both Tables", tableFail = tableFail||" Table"
	);
	New Window( tableFail||"_Incomplete Column Selection",
		<<Modal,
		Text Box( "Naming issue with "||tableFail, <<setfontstyle( "bold" ), <<setfontsize( 12 ) ),
		Text Box( "One or more columns are missing or incorrect for data type", <<setfontstyle( "bold" ), <<setfontsize( 10 ) ),
		Text Box( "" ), 

		Text Box( "Please be sure the following nomenclature stadards are being adhered to", <<setfontstyle( "bold" ) ),
		Text Box( "\!"Angle\!" = Analyzer response column" ),
		Text Box( "\!"PROG Conc\!" = Reference dose column" ),
		Text Box( "\!"conjugateTiter\!" = Titer dose column" ),
		Button Box( "OK" ),

	)
);
failedVerificationEvent = Function({},{default local},
	New Window( "Failed Verification Event",
		<<Modal,
		Text Box( "Response verification failed", <<setfontstyle( "bold" ), <<setfontsize( 12 ) ),
		Text Box( "" ), 
		Text Box( "Retest may be needed or contact supervisor or R&D", <<setfontstyle( "bold" )),
		Button Box( "OK" ),
	)
);
successfulVerificationEvent = Function({},{default local},
	New Window( "Successful Titer Event",
		<<Modal,
		Text Box( "Response Verification Passed", <<setfontstyle( "bold" ), <<setfontsize( 12 ) ),
		Button Box( "OK" ),
	)
);

//startDil = 6; // 5:1 = 6x = 6, 7:1 = 8x = 8 and so on

doseStartValues = nrcSequence( 0, 40, 0.5 );



//sets up for all columns to be added with formulas
startFormula2 = "titerDT<<newcolumn(\!"AR(Dose)\!", Formula(Match(:Name(\!"Ord(conjugateTiter)\!"),";
startFormula4 = "titerDT<<newcolumn(\!"1stDeriv_AR(Dose)\!", Formula(Match(:Name(\!"Ord(conjugateTiter)\!"),";
startFormula5 = "titerDT<<newcolumn(\!"AR SD (Dose)\!", Formula(";
startFormula6 = "titerDT<<newcolumn(\!"Dose SD(Dose)\!", Formula(";

endForm = ")))";
arSDendForm = "))";

arSDform = "(1.2046957598E-04 + 4.6037745001E-02 * :Name( \!"AR (Dose)\!" ) +
	-5.8460259381E-01 * (:Name( \!"AR (Dose)\!" ) ^ 2))";

dt_start = currentdatatable();
startName = dt_start << GetName();
If(Contains(startName,"_resp_") > 0,
	fullMonty = 0;
	accept01 = 0.18;
	accept05 = 0.6;
	accept20 = 2.4;
	,
	fullMonty = 1;
	accept01 = 0.15;
	accept05 = 0.5;
	accept20 = 2;
	dt_titer = Open();
);

Try(IsScriptable(Column(dt_start,"conjugateTiter"));
	IsScriptable(Column(dt_start,"PROG Conc"));
	IsScriptable(Column(dt_start,"Angle"));
,
	missingCols("Response Verification");
	Try(Close(dt_titer,nosave));
	throw();
);

dt = dt_start << Subset(Output Table( startName ), All Rows,Selected columns only( 0 )/*,invisible*/);

For(i=1,i<=nrow(dt),i++,
	If(Column(dt,"PROG Conc")[i] == 0,
		Column(dt,"PROG Conc")[i] = 0.0001 // Change to 1?
	,
		Empty()
	)
);

Summarize(dt,titerList = by(Column("conjugateTiter")));
/*NO NEED TO ANCHOR
For(j=1,j<=nitems(titerList),j++,
	For(i=1, i<=100,i++,
		dt << Add Rows( {:Name("PROG Conc") = 10000, :Name("Angle") = 0.0001, :Name("conjugateTiter") = Num(titerList[j])})
	)
);
//Column(dt,"Concentration Ratio") << Suppress formula eval(0);
*/
doseCurve = dt << Fit Curve( 
	Y( :Name("Angle") ), 
	X( :Name("PROG Conc") ), 
	Fit Logistic 4P Rodbard,
	By(:Name("conjugateTiter"))
);
//doseCurve << (Fit[1] << save prediction formula); //May not be needed with all the other stuff.
If(fullMonty == 1,
	combCurves = dt_start << Bivariate( 
		Y( :Name("Angle") ),
		X( :Name("PROG Conc") ),
		SendToReport(
			Dispatch(
				{},
				"Bivariate Fit of Angle By PROG Conc",
				OutlineBox,
				{Set Title(
					"Actual Response Curve (Black) vs Predicted Response Curve (Blue)"
				)}
			),
			Dispatch(
				{},
				"1",
				ScaleBox,
				//UPDATE FOR PROG
				{Min( -1 ), Max( 40 ), Inc( 5 ), Minor Ticks( 1 )}
				/////////
			),
			Dispatch(
				{},
				"Bivar Plot",
				FrameBox,
				{Frame Size( 384, 329 ), Grid Line Order( 6 ), Reference Line Order( 7 )}
			)
		)
	);
	combCurves << show window(0);
);

If(nitems(titerList) == 1,
	params = Report(doseCurve)[Tablebox(3)] << make combined datatable;
	params << show window(0);
,
	params = Report(doseCurve[1])[Tablebox(3)] << make combined datatable;
	params << show window(0);
);

For(i=ncol(params),i>=1,i--,
	a = Column(params,i) << getname();
	
	If(a != "Parameter" & a != "Estimate" & a != "conjugateTiter",
		//Print("deleting");
		//For(j=1, j<=nitems(params),j++,
			params << DeleteColumns(a);
		//)
	,
		Empty()
	)
);
/* Rodbard Normal Form (for deriv calculations):([c-L asympt]) + ([d-U asympt] - ([c-L asympt])) / (1 + ([Concentration] / [b-inflection]) ^ [a-Growth])*/
fitformula = {};
fitDerivForm = {};
For( i = 1, i <= N Items( titerList ), i++,
	r = Eval Insert( "\!"^titerList[i]^\!"" );
	fitFormula[i] = r || "," || Char(
		Eval Expr(
			Expr( params[(4 * i - 1), 3] ) + (Expr( params[(4 * i), 3] ) - Expr( params[(4 * i - 1), 3] )) / (1 + (:Name( "Dose" ) /
			Expr( params[(4 * i - 2), 3] )) ^ Expr( params[(4 * i - 3), 3] ))
		)
	);
	fitDerivForm[i] = r || "," || Char(
		Eval(
			Eval Expr(
				Derivative(
					Expr( params[(4 * i - 1), 3] ) + (Expr( params[(4 * i), 3] ) - Expr( params[(4 * i - 1), 3] )) / (1 + (:Name( "Dose" ) / Expr( params[(4 * i - 2), 3] )) ^ Expr( params[(4 * i - 3), 3] ))
				,
					:Name( "Dose" )
				)
			)
		)
	);
);
fitConcatFormula = ConcatItems(fitFormula,",");
fitConcatDerivForm = ConcatItems(fitDerivForm,",");

//adds rodbard lines to original data
If(fullMonty == 1,
	For( i = 1, i <= N Items( titerList ), i++,
		Eval(
			Eval Expr(
				Report( combCurves )[FrameBox( 1 )] << Add Graphics Script(
					1,
					Description( "Titer_"||Expr(titerList[i]) ),
					Y Function(
						Expr( params[(4 * i - 1), 3] ) + (Expr( params[(4 * i), 3] ) - Expr( params[(4 * i - 1), 3] )) / (1 + (x /
				Expr( params[(4 * i - 2), 3] )) ^ Expr( params[(4 * i - 3), 3] ))
					,
						x
					)
				);
			)
		)
	);
);
Close(params, nosave);
Close(dt, nosave);
//doseCurve << Close window(); (breaks jmp 15 'cause dt closes report when it closes')

doseSDform = Concat("-((1 / Match(:Name(\!"Ord(conjugateTiter)\!"),",fitConcatDerivForm,")) *", arSDform, ")");

//fullFitform = Concat(startFormula, fitFormula, endForm);
//fullARdoseSub0_5form = Concat(startFormula1, midForm1, subForm1, endForm);
fullARdoseForm = Concat(startFormula2, fitConcatFormula, endForm);
//fullARdoseAdd0_5form = Concat(startFormula3, midForm3, subForm3, endForm);
fullSlopeForm = Concat(startFormula4,fitConcatDerivForm,endForm);
fullARsdForm = Concat(startFormula5,arSDform,arSDendForm);
//fullDoseSDform = Concat(startFormula6,doseSDForm,arSDendForm);

titerDT1 = New Table("Titers",New Column("conjugateTiter",set values(titerList)),private);
titerDT2 = New Table("Doses",New Column("Dose",set values(doseStartValues)),private);

If( fullMonty == 1, titerDT = titerDT1 << Join( With( titerDT2 ), Cartesian Join,private);
	,
	fullMonty == 0, titerDT = titerDT1 << Join( With( titerDT2 ), Cartesian Join);
);

Close(titerDT1,nosave);
Close(titerDT2,nosave);

titerDT << new column ("Ord(conjugateTiter)", Ordinal, Character(10),
	Formula(Char(Round(:Name("conjugateTiter"),3)))
);
Column(titerDt,"Ord(conjugateTiter)") << deleteformula();

//Eval( Parse( fullFitForm ) ); //puts 'angle predictor' in base table (combine with all evals later)
//Eval( Parse( fullARdoseSub0_5form ));
Eval( Parse( fullARdoseForm ));
//Eval( Parse( fullARdoseAdd0_5form ));

Eval( Parse( fullSlopeForm ));
Eval( Parse( fullARsdForm ));
//Eval( Parse( fullDoseSDForm ));

titerDT << New Column("Dose SD(Dose)",
	Formula(
		-(:Name( "AR SD(Dose)" ) * (1 / :Name( "1stDeriv_AR(Dose)" )))
	)
);
titerDT << New Column("Data Type",
	Formula("Dose Response")
);
Column(titerDT,"Data Type")<<deleteformula();

If(fullMonty == 1, titerConcat = titerDT << Concatenate(dt_titer);
	,
	fullMonty == 0, titerConcat = titerDT
);

titerConcat << Set Name("Response Verification Table");

Column(titerConcat,"Ord(conjugateTiter)") << Set Formula(Round(:Name("conjugateTiter"),3));
Column(titerConcat,"Ord(conjugateTiter)") << delete formula();
Column(titerConcat,"Ord(conjugateTiter)") << Data Type("Character");

For(i=1, i<= nrow(titerConcat),i++,
	Row State(i) = combine states(Marker State(0),Color State(0));
);

For(i=1, i<=nitems(titerList),i++,
	rowMtrx = titerConcat << get rows where(:Name("Ord(conjugateTiter)") == titerList[i]);
	For(j=1,j<=nrow(rowMtrx),j++,
		Row State(rowMtrx[j]) = Marker State(i + 3)
	)
);

For Each Row(titerConcat,
	If(:Name("Dose") == 1.0 | :Name("Dose") == 5.0 | :Name("Dose") == 20.0,
		RowState() = CombineStates(SelectedState(0),MarkerState(16),ColorState(3));

	)
);
Summarize(titerConcat, a = by(:Name("Dose"),:Name("Data Type")), c = Mean(:Name("Dose SD(Dose)")));
oneLocale = Contains(a[1], "1");
fiveLocale = Contains(a[1], "5");
twentyLocale = Contains(a[1], "20");

If(0 < c[oneLocale] <= accept01 & 0 < c[fiveLocale] <= accept05 & 0 < c[twentyLocale] <= accept20,
	responseCheck = "Pass"
,
	responseCheck = "Fail"
);
If(fullMonty == 1,
	quickTable = titerConcat << Subset( All rows, Selected columns only( 0 ));
	quickTable << SelectWhere( Contains(titerList,:Name( "Ord(conjugateTiter)" ))==0 ) << deleterows;
	quickTable << SelectWhere( IsMissing(:Name("Growth Rate(conjugateTiter)")) | :Name("Dose") != 0 ) << deleterows;

	For Each Row(
		quickTable,
		Eval(
			Eval Expr(
				Report( combCurves )[FrameBox( 1 )] << Add Graphics Script(
					1,
					Description( "ModeledTiter_"||Char(Expr(Column(quickTable,"conjugateTiter")[Row()])) ),
					Pen Color("Blue");
					Y Function(
						Expr( quickTable[Row(),13] ) + (Expr( quickTable[Row(),14] ) - Expr( quickTable[Row(),13] )) / (1 + (x /
				Expr( quickTable[Row(),12] )) ^ Expr( quickTable[Row(),11] ))
					,
						x
					)
				)
			)
		);
	);
	Close(quickTable,nosave);
);

If(fullMonty == 1,
	predictionTitle = "DoseSD Predictions: Response Ver (Red), Original Model (Blue)";
	particleSpec = "";
	,
	fullMonty == 0,
	predictionTitle = "DoseSD Predictions: Response Verification";
	particleSpec = "Text({1,3.25},\!"DashDot line is Particle Qualification Specification\!");
		Line Style(\!"DashDot\!");
		Line( {0, 0.18}, {1, 0.18}, {3, 0.36}, {6, 0.72}, {12, 1.44}, {20, 2.4}, {30, 3.6}, {40, 4.8} )"
);
//particleSpec = "hi";
Eval(
	EvalExpr(
		biv1 = titerConcat << Bivariate(
			//By(Expr(particleSpec)),
			Y( :Name( "Dose SD (Dose)" ) ),
			X( :Name("Dose") ),
			SendToReport(
				Dispatch(
					{},
					"2",
					ScaleBox,
					{Min( 0 ), Max( 3.5 ), Inc( 0.25 ), Minor Ticks( 1 )}
				),
				Dispatch(
					{},
					"1",
					ScaleBox,
					{Min( 0 ), Max( 40 ), Inc( 2 ), Minor Ticks( 1 )}
				),
				Dispatch(
					{},
					"Bivar Plot",
					FrameBox,
					{Frame Size( 450, 368 ),
					Add Graphics Script(
						4,
						Description( "Precision Req" ),
						Pen Size( 2 ); 
						Pen Color( "blue" ); 
						Line( {0, 0.1}, {1, 0.1}, {3, 0.2}, {6, 0.3}, {12, 0.7}, {20, 1.25}, {30, 2}, {40, 2.75} ); 
						Pen Color( "red" ); 
						Line( {0, 0.15}, {1, 0.15}, {3, 0.3}, {6, 0.6}, {12, 1.2}, {20, 2}, {30, 3}, {40, 4} );
						Expr(Parse(particleSpec))
					), Row Legend(
						Name("Data Type"),
						Color( 1 ),
						//Color Theme( "Jet" ),
						Marker( 0 ),
						Marker Theme( "" ),
						Continuous Scale( 0 ),
						Reverse Scale( 0 ),
						Excluded Rows( 0 )
					)
					}
				),
				Dispatch(
					{},
					"Bivariate Fit of Dose SD(Dose) By Dose",
					OutlineBox,
					{Set Title( predictionTitle )}
				)
			)
		);
	)
);
biv1 << Local Data Filter(
	Auto clear( 0 ),
	Add Filter(
		columns( :Name( "Ord(conjugateTiter)" ) ),
		Where( :Name( "Ord(conjugateTiter)" ) == titerList[1] ),
		Display( :Name( "Ord(conjugateTiter)" ), Size( 160, 225 ), List Display ),
		//Order By Count( :Name( "Ord(conjugateTiter)" ) 
	)
);
wait(0);F
nw = New Window("Response Verification Data", <<Journal,

	vlb = V List Box(
		
	)
);
If(fullMonty == 1, vlb << Append(Report(combCurves)));
vlb << Append(Report(biv1));
titerSub = titerDT << selectwhere(:Name("Dose") == 1 | :Name("Dose") == 5 | :Name("Dose") == 20 ) << Subset("Subset",SelectedRows(1),private);
titerDT << select all rows;
titerDT << invert row selection;
titerTab = titerSub << Tabulate(
	Change Item Label( Statistics( Sum, "Data" ) ),
	Show Control Panel( 0 ),
	Add Table(
		Column Table(
			Analysis Columns( :Name( "AR (Dose)" ), :Name( "Dose SD (Dose)" ) )
		),
		Row Table( Grouping Columns( :Name( "Ord(conjugateTiter)" ), :Dose ) )
	),
	SendToReport( Dispatch( {}, "Tabulate", OutlineBox, {Set Title( "AR and DoseSD Metrics" )} ) )
);
vlb << Append(Report(titerTab));
Try(combCurves << close window);
Close(titerSub,nosave);
If(fullMonty == 1, 
	Close(dt_titer,nosave);
	Close(titerDT,nosave);
);
Match(responseCheck,
	"Pass",
	successfulVerificationEvent();
	titerConcat << New Column("Disposition",
		Formula("Passed")
	);
	Column(titerConcat,"Disposition") << deleteformula
,
	"Fail",
	failedVerificationEvent();
	titerConcat << New Column("Disposition",
		Formula("Failed")
	);
	Column(titerConcat,"Disposition") << deleteformula
);

