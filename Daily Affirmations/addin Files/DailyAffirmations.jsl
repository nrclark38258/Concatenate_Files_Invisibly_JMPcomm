//NClark

//	1.1 NClark 040822
//		-> updated so table can be saved to THIS addin and not originating one
//

Names Default to here(1);

//-----------Variable Declarations (above Main for easy access)--

	//gathers user information
	If(HostIs("MAC"),
		u = Get Environment Variable("USER");
		,
		u = Get Environment Variable("USERNAME");
	);
	addinID = "com.idexx.nclark.dailyAffirmations";
	minJMPver = "14";
	
//-----------/Variable Declarations-----------------------

include("$ADDIN_HOME(com.idexx.nclark.dailyAffirmations)\Utilities\utilities.jsl");

checkVersion = Function( {minVer},
	{default local},
	currVer = JMP Version();
	currWords = words(currVer,".");
	minWords = words(minVer,".");
	minBackHalf = If(nitems(minWords)==1,"0",concatitems(minWords[2::nitems(minWords)],""));
	backHalf = If(nitems(currWords)==1,"0",concatitems(currWords[2::nitems(currWords)],""));
	newNumVer = Num(currWords[1]||"."||backHalf);
	newMinVer = Num(minWords[1]||"."||minBackHalf);
	
	If(newNumVer >= newMinVer, Return(1),Return(0));
);

wrongVer = Function( {maxVer},
	{Default Local},
	New Window( "Requires JMP " || Char( maxVer ),
		<<Modal,
		Text Box( "This addin requires the used of JMP " || Char( maxVer ) || "+" ),
		Button Box( "OK" )
	)
);

dailyAffirmation = Function({user},{default local},
	//user = "cduffey";
	
	Try(
		dt = Open("\\FOGHORN.namerica.idexxi.com\Groups\JMP User Group\JMP Addins\Daily Affirmations\Daily Affirmations.jmp",private);
		dt << Save("$ADDIN_HOME(com.idexx.nclark.dailyAffirmations)\Daily Affirmations.jmp");
		,
		dt = Open("$ADDIN_HOME(com.idexx.nclark.dailyAffirmations)\Daily Affirmations.jmp",private);
	);	
	
	dt << select where(:"Name"n == user | :"Name"n == "") << invert row selection << delete rows;
	
	rand = randominteger(1,nrows(dt));
	comment = dt:"UpliftingComment"n[rand];
	dt << close window();
	Return(comment);
);

dailyAffMsg = Function({comment},{default local},
	//missingList = {"Spec PL (µg/L)", "Slide Condition"};
	New Window( "Starting off the analysis right!",
		//<<Modal,
		Show Menu(0),
		Show Toolbars(0),
		HlistBox(
		Text Box( "    " ),
		Text Box( comment, <<setfontstyle( "bold" ), <<setfontsize( 36 ), << set wrap(800) ),
		Text Box( "    " )
		)
	);
	
);

////////////////////////////////////////
//                                    //
//              Main                  //
//                                    //
////////////////////////////////////////

If( Length( Include File List() ) == 1,
	trackUsage(u,addinID);

	If( checkVersion( minJMPver ) == 1,

		dailyAff = dailyAffirmation(u);
		dailyAffMsg(dailyAff);
		,
		wrongVer( minJMPver );
	)
	,
	Print( "quantPL Dose Model scipt was included from another source" );
);